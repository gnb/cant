dnl Process this file with autoconf to produce a configure script.
dnl You may need the --localdir option to get the included .m4's
dnl
dnl CANT - A C implementation of the Apache/Tomcat ANT build system
dnl Copyright (c) 2001 Greg Banks
dnl 
dnl This program is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation; either version 2 of the License, or
dnl (at your option) any later version.
dnl 
dnl This program is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl 
dnl You should have received a copy of the GNU General Public License
dnl along with this program; if not, write to the Free Software
dnl Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
dnl 
dnl $Id: configure.in,v 1.8 2001-11-16 04:22:41 gnb Exp $
dnl

AC_INIT(src/cant.c)
dnl sinclude(/usr/share/aclocal/gettext.m4)dnl
dnl sinclude(/usr/share/aclocal/lcmessage.m4)dnl
sinclude(/usr/share/aclocal/init.m4)dnl
sinclude(/usr/share/aclocal/header.m4)dnl
sinclude(/usr/share/aclocal/progtest.m4)dnl
sinclude(/usr/share/aclocal/missing.m4)dnl
sinclude(/usr/share/aclocal/sanity.m4)dnl
sinclude(/usr/share/aclocal/cond.m4)dnl
sinclude(/usr/share/aclocal/glib.m4)dnl
sinclude(libxml.m4)dnl
AM_INIT_AUTOMAKE(cant,0.0.5)
AM_CONFIG_HEADER(config.h)


dnl Enabling debugging code
dnl This is mostly straight out of the automake manual.
AC_MSG_CHECKING(whether to enable debugging code)
AC_ARG_ENABLE(debug,
[  --enable-debug          Turn on debugging code],[
case "${enableval}" in
yes|no)
    debug=${enableval}
    ;;
*)
    AC_MSG_ERROR(bad value ${enableval} for --enable-debug)
    ;;
esac
],[debug=no])
AC_MSG_RESULT($debug)
test x$debug = xyes && CFLAGS="-g"
AM_CONDITIONAL(DEBUG, test x$debug = xyes)


dnl Checks for programs.
AC_ARG_PROGRAM
AC_PROG_INSTALL
AC_PROG_AWK
AC_PROG_CC

dnl Stuff for gettext
dnl ALL_LINGUAS="la"
dnl AM_GNU_GETTEXT

dnl Checks for libraries.

dnl Note that we want to override the side
dnl effect of adding the library to $LIBS.
AC_CHECK_LIB(pthread,pthread_create,[dummy=1],[dummy=0])

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(malloc.h sys/ioctl.h sys/time.h unistd.h memory.h)
AC_CHECK_HEADERS(signal.h sys/filio.h pthread.h semaphore.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_PID_T

dnl Check for the libxml library
AM_PATH_LIBXML(1.8.7)
AM_PATH_GLIB(1.2.7)

dnl Check the threads implementations
AC_MSG_CHECKING(for best threads implementation)
AC_ARG_WITH(threads,
[  --with-threads=WHICH     Which threads implementation to use (none,posix)],
[
# Parse user's requested threads implementation, even
# if it appears to be unachievable from our earlier probe
# of libraries and headers.
case "$withval" in
no)
    # user specified --without-threads
    THREADS=none
    ;;
none|posix)
    THREADS=$withval
    ;;
*)
    AC_MSG_ERROR([Unknown argument for --with-threads, expecting \"none\" or \"posix\"])
    ;;
esac
],[
# Automatically infer the most appropriate threads
# implementation from probed headers and libraries
if test "${ac_cv_header_pthread_h}:${ac_cv_header_semaphore_h}:${ac_cv_lib_pthread_pthread_create}" = yes:yes:yes ; then
    THREADS=posix
else
    THREADS=none
fi
])
AC_MSG_RESULT($THREADS)

THREADS_NONE=0
THREADS_POSIX=0
case "$THREADS" in
none)
    THREADS_NONE=1
    ;;
posix)
    THREADS_POSIX=1
    THREADS_LIBS="-lpthread"
    ;;
esac
AC_DEFINE_UNQUOTED(THREADS_NONE, $THREADS_NONE)
AC_DEFINE_UNQUOTED(THREADS_POSIX, $THREADS_POSIX)
AC_SUBST(THREADS_CFLAGS)
AC_SUBST(THREADS_LIBS)


dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_TYPE_SIGNAL
AC_FUNC_WAIT3
AC_CHECK_FUNCS(putenv regcomp strchr)

AC_DEFINE_UNQUOTED(PACKAGE, "$PACKAGE")
AC_DEFINE_UNQUOTED(VERSION, "$VERSION")
AC_DEFINE(HAVE_WAIT3)
dnl AC_SUBST(ALL_LINGUAS)
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)
AC_OUTPUT(Makefile src/Makefile)
dnl intl/Makefile po/Makefile.in
